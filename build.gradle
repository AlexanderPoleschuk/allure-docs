import com.bmuschko.gradle.docker.tasks.image.Dockerfile
import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import com.bmuschko.gradle.docker.tasks.image.DockerPushImage
import com.bmuschko.gradle.docker.tasks.image.DockerRemoveImage

buildscript {
    repositories {
        mavenLocal()
        jcenter()
        maven { url 'https://plugins.gradle.org/m2/' }
    }

    dependencies {
        classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.3'
        classpath 'com.bmuschko:gradle-docker-plugin:3.0.12'
    }
}

ext {
    gradleScriptDir = "${rootProject.projectDir}/gradle"
    imageTag = project.hasProperty("imageTag") ? project.property("imageTag") : "latest"
}

apply from: "${gradleScriptDir}/docker.gradle"
apply from: "${gradleScriptDir}/asciidoctor.gradle"

build.finalizedBy(asciidoctor)

task buildDockerImage(type: DockerBuildImage) {
    inputDir project.projectDir
    tag "allure/${project.name}:${imageTag}"
}

task pushDockerImage(dependsOn: buildDockerImage, type: DockerPushImage) {
    imageName "allure/${project.name}"
    tag imageTag
}

task publishDockerImage() {
    dependsOn buildDockerImage, pushDockerImage
}
